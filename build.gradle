buildscript {
    ext.kotlin_version = '1.1.0-rc-91'

    repositories {
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
}

subprojects {
    group 'com.lloydramey'
    version '0.0.1'

    apply plugin: 'kotlin'
    apply plugin: 'com.github.hierynomus.license'

    license {
        header rootProject.file("APACHE_HEADER")
        mapping 'kt', 'SLASHSTAR_STYLE'
        mapping 'java', 'SLASHSTAR_STYLE'
        strictCheck true

        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.name = 'Lloyd Ramey'
        ext.email = 'lnr0626@gmail.com'
    }

    configurations {
        ktlint
    }

    repositories {
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
        mavenCentral()
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.4.0'
        // additional 3rd party ruleset(s) can be specified here
        // just add them to the classpath (ktlint 'groupId:artifactId:version') and
        // ktlint will pick them up
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    check.dependsOn ktlint

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }
}