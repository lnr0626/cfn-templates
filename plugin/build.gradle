plugins {
    id "java-gradle-plugin"
}

gradlePlugin {
    plugins {
        cfnPlugin {
            id = "com.lloydramey.cfn"
            implementationClass = "com.lloydramey.cfn.gradle.plugin.CfnPlugin"
        }
    }
}
// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()
    compile project(":base-models")
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.23'

    testCompile project(":aws")
    testCompile gradleTestKit()
    testCompile "junit:junit:4.12"
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    testRuntime files(createClasspathManifest)
}

task copyLibs(type: Copy) {
    from configurations.runtime
    into 'build/libs'
}
